# diamond-kata

```
The diamond-kata of 'A'
 A

The diamond-kata of 'B'
  A
 B B
  A

The diamond-kata of 'C'
   A
  B B
 C   C
  B B
   A
   
 and so on...
```

How to implement this? It turns out, that there is a certain symmetry in the above diamonds. The symmetry behaves equally in both the vertical and the horizontal direction.

So, when using the third example from above (the diamond of 'C'), it's sufficient to model one quarter of the structure and do some mirroring afterwards:

```
  A
 B 
C  
```
It's important to get the letters padded properly (here with spaces) on both sides. It's getting clearer when not using spaces here:

```
--A
-B- 
C--
```
So if the input is a list (e.g. of characters) and a padding element, the function producing a quarter could look like this:

```haskell
quarter :: a -> [a] -> [[a]]
quarter y xs =
  zipWith f xs [0..]
  where
    f x i = concat
      [ replicate (length xs-1-i) y
      , [x]
      , replicate i y
      ]
 ```

The function basically maps/zips over the passed list and some indices starting at zero. On each iteration it concatenates the paddings (y) before and after the item. The paddings are generated by the `replicate` function, that constructs a list of a given length containing a constant value. We use the index to calculate the proper padding lengths.

When invoking this function with a characters `quarter '-' ['A','B','C']` we get `["--A","-B-","C--"]` which is what we need. It turned out that this was already the hardest part of the implementation.

The next thing we have to do is to do the mirroring. For this I created a helper function called `joinMirrored`

```haskell
joinMirrored :: [a] -> [a]
joinMirrored xs =
  xs ++ tail (reverse xs)
```

Nothing special here, just a function that glues a list to its reversed counterpart, while avoiding to have the element doubled in the middle.

```haskell
joinMirrored [1,2,3]
--> [1,2,3,2,1]
```

Since the `quarter` function returns a list of lists, we need to map over the outer list in order to affect the inner one, in other words it's columns:


```
  A
 B 
C  
```
`map joinMirrored (quarter ' ' ['A'..'C'])`
```
  A  
 B B 
C   C
```
Almost done, we now just have to apply the same function to the result:

`joinMirrored it`
```
  A  
 B B 
C   C
 B B 
  A  
```

finally the `diamondKata` function looks like this:

```haskell
diamondKata :: a -> [a] -> [[a]]
diamondKata y xs =
    joinMirrored
  . map joinMirrored
  . quarter y 
  $ xs
```

It's just a function composition of the above bits.

Even though I used strings and characters as examples, the above functions where defined in general for lists. The structure is meant for printing and works best with characters, so finally there's a wrapping function that makes the whole thing string specific and uses the actual space as padding:

```haskell
diamondKataStr :: String -> String
diamondKataStr xs =
  unlines $ diamondKata ' ' xs
```

